apply plugin: 'maven-publish'
apply plugin: 'signing'


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                artifact("radixmobile.aar")

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "SendingNetwork Android SDK Radix"
                    url = 'https://github.com/LinX-OpenNetwork/sendingnetwork-Android-sdk'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'jamescooper'
                            name = 'jamescooper'
                            email = 'jamescooper@sending.network'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git@github.com:LinX-OpenNetwork/sendingnetwork-Android-sdk.git'
                        developerConnection = 'scm:git@github.com:LinX-OpenNetwork/sendingnetwork-Android-sdk.git'
                        url = 'https://github.com/LinX-OpenNetwork/sendingnetwork-Android-sdk'
                    }
                }

            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}
